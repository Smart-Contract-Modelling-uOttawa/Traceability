grammar org.xtext.dsl.dxl.DxlDsl with org.eclipse.xtext.common.Terminals

generate dxlDsl "http://www.xtext.org/dsl/dxl/DxlDsl"


Model:
	'model' name=ID '{'
	(folders+=Folder)*	
	'}'
;
	

Folder: 
	'folder' name=ID '{'
	(module+=Module)*
	(associationType+=AssociationType)*  // These are link modules
	
	'}'
;

Module:
	'module' (ignoreInReport?='ignoreInReport')? name=ID '{'
	
	//Option to declare a file name: default convention used 'Maps' for map, 'Devices' for 'device'
	('fileName' fileName=STRING)?
	
	(classes+=Class)*		
	
	'}'
;
	
Class:
	'class' (noDescription?='noDescription')? name=ID ('shows as' classTypeDescription=STRING)? '{'
	
	(attributes+=Attribute)*
	
	(associations+=Association)*
	
	
	'}'
	;

Attribute:
		
	(ignored?='ignored')? type=DataType name=STRING ('shows as' default=STRING)?
	
;

DataType:
	'bool' | 'string' | 'int' | 'text' | 'diagram'
	// Note: not more than 1 Diagram attribute per class
;


AssociationType:
	'associationType' name=ID linkFileName=STRING
;

Association:	
	'association' name=ID':'assoType=[AssociationType] 'to' moduleType=STRING('.'classType=STRING)? 
	(assoDescription=STRING)?
	
;



